plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.imranrasulzade'
version = '1.0.0'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
    withJavadocJar()
}

repositories { mavenCentral() }

dependencies {
    api "org.springframework:spring-web:6.1.6"

    api "org.springframework.boot:spring-boot-autoconfigure:3.2.5"

    api "org.springframework.boot:spring-boot-starter-validation:3.2.5"

    api "org.springframework.boot:spring-boot-starter-aop:3.2.5"

    compileOnly "jakarta.servlet:jakarta.servlet-api:6.0.0"
    testImplementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'


    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"
}

tasks.test { useJUnitPlatform() }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "exception-core-lib"

            pom {
                name = "exception-core-lib"
                description = "Production-ready exception handling library for Spring Boot (i18n, traceId, AOP logging, auto-configuration)."
                url = "https://github.com/imranrasulzade/exception-core-lib"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = "imranrasulzade"
                        name = "Imran Rasulzade"
                        email = "rasulzade002@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/imranrasulzade/exception-core-lib.git"
                    developerConnection = "scm:git:ssh://github.com:imranrasulzade/exception-core-lib.git"
                    url = "https://github.com/imranrasulzade/exception-core-lib"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        }
    }

}

//signing {
//    def key = findProperty("signingKey")
//    def pass = findProperty("signingPassword")
//
//    if (key != null && key.toString().trim()) {
//        useInMemoryPgpKeys(
//                key.toString().replace("\\n", "\n"),
//                pass != null ? pass.toString() : ""
//        )
//        sign publishing.publications.mavenJava
//    }
//}
signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}


tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    failOnError = false
}

